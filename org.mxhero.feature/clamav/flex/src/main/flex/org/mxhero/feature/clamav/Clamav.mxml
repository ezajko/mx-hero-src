<?xml version="1.0" encoding="utf-8"?>
<mx:Module xmlns:fx="http://ns.adobe.com/mxml/2009" 
		   xmlns:s="library://ns.adobe.com/flex/spark" 
		   xmlns:mx="library://ns.adobe.com/flex/mx" 
		   layout="vertical"
		   creationComplete="creationCompleteHandler(event)"
		   verticalGap="20"
		   paddingTop="30" 
		   xmlns:component="org.mxhero.console.commons.feature.component.*"
		   implements="org.mxhero.console.commons.feature.IFeature">
	
	<fx:Declarations>
		<s:RadioButtonGroup id="action"/>
	</fx:Declarations>
	
	<fx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			import mx.events.FlexEvent;
			import mx.utils.StringUtil;
			
			import org.mxhero.console.commons.feature.FeatureRuleProperty;
			import org.mxhero.console.commons.feature.IFeature;
			import org.mxhero.console.commons.feature.PropertyHelper;
			
			private var initialProperties:ArrayCollection;
			
			protected function creationCompleteHandler(event:FlexEvent):void
			{
				styleManager.loadStyleDeclarations("styles/mxhero.swf",true,false,ApplicationDomain.currentDomain);
				returnTa.text=resourceManager.getString('clamav','return.default.message');
				loadData();
			}
			
			public function isValid():Boolean{
				if(returnRb.selected){
					if(returnTa.text==null || StringUtil.trim(returnTa.text).length<1){
						returnTa.errorString=resourceManager.getString('clamav','return.message.error');
						return false;
					}
				}
				returnTa.errorString="";
				return true;
			}
			
			public function setProperties(data:ArrayCollection):void{
				initialProperties=data;
			}
			
			private function loadData():void{
				if(initialProperties!=null){
					var selectedAction:String=PropertyHelper.getProperty(initialProperties,'action.selection').propertyValue;
					if(selectedAction=='reject'){
						dropRb.selected=true;
					} else if(selectedAction=='return'){
						returnRb.selected=true;
						returnTa.text=PropertyHelper.getProperty(initialProperties,'return.message').propertyValue;
					}
				}
			}
			
			public function getProperties():ArrayCollection{
				var properties:ArrayCollection = new ArrayCollection();
				var property:FeatureRuleProperty;
				
				property = new FeatureRuleProperty();
				property.propertyKey = "action.selection";
				
				if(dropRb.selected){
					property.propertyValue="reject";
				} else if(returnRb.selected){
					property.propertyValue="return";
					var returnMessage:FeatureRuleProperty = new FeatureRuleProperty();
					returnMessage.propertyKey="return.message";
					returnMessage.propertyValue=returnTa.text;
					properties.addItem(returnMessage);
				}

				properties.addItem(property);
				
				return properties;
			}
			
			public function getValidationError():String{
				return returnTa.errorString;
			}
			
			public function fixedTo():Boolean{
				return false;
			}
			
			public function fixedFrom():Boolean{
				return false;
			}
			
			public function fixedToValue():String{
				return "";
			}
			
			public function fixedFromValue():String{
				return "";
			}
		]]>
	</fx:Script>
	
	<fx:Metadata>
		[ResourceBundle("clamav")]
	</fx:Metadata>
	
	<s:Label text="{resourceManager.getString('clamav','first.title')}" fontSize="16" fontWeight="bold"/>
	
	<s:VGroup width="100%" gap="10">
		<s:HGroup width="100%" verticalAlign="top">
			<s:RadioButton id="dropRb"
						   groupName="action"
						   label=""
						   selected="true"/>
			<component:LabelExpanded
				paddingTop="5"
				labelText="{resourceManager.getString('clamav','reject.label.text')}"
				expandedText="{resourceManager.getString('clamav','reject.label.expanded')}"
				explainText="{resourceManager.getString('clamav','label.explain')}"
				hideText="{resourceManager.getString('clamav','label.hide')}"
				width="100%"/>
		</s:HGroup>
	</s:VGroup>
	<s:VGroup width="100%">
		<s:HGroup width="100%">
			<s:RadioButton id="returnRb" 
						   groupName="action"
						   label=""/>
			<component:LabelExpanded
				paddingTop="5"
				labelText="{resourceManager.getString('clamav','return.label.text')}"
				expandedText="{resourceManager.getString('clamav','return.label.expanded')}"
				explainText="{resourceManager.getString('clamav','label.explain')}"
				hideText="{resourceManager.getString('clamav','label.hide')}"
				width="100%"/>			
		</s:HGroup>
		<s:VGroup paddingLeft="24" width="100%">
			<s:TextArea id="returnTa"
						editable="true" 
						width="350"
						enabled="{returnRb.selected}"/>			
		</s:VGroup>
	</s:VGroup>
	
</mx:Module>
