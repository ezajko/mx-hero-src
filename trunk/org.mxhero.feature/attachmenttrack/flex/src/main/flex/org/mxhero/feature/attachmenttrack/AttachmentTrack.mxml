<?xml version="1.0" encoding="utf-8"?>
<s:Module xmlns:fx="http://ns.adobe.com/mxml/2009" 
		   xmlns:s="library://ns.adobe.com/flex/spark" 
		   xmlns:mx="library://ns.adobe.com/flex/mx" 
		   creationComplete="creationCompleteHandler(event)"
		   implements="org.mxhero.console.commons.feature.IFeature" xmlns:tlf="net.asciiworks.tlf.*">
	
	<fx:Declarations>
	</fx:Declarations>
	
	<fx:Script>
		<![CDATA[
			import flashx.textLayout.conversion.ConversionType;
			import flashx.textLayout.conversion.TextConverter;
			
			import mx.collections.ArrayCollection;
			import mx.events.DragEvent;
			import mx.events.FlexEvent;
			import mx.formatters.NumberFormatter;
			import mx.managers.DragManager;
			import mx.messaging.messages.ErrorMessage;
			import mx.resources.ResourceManager;
			import mx.utils.StringUtil;
			import mx.validators.NumberValidatorDomainType;
			
			import org.mxhero.console.commons.feature.FeatureRuleProperty;
			import org.mxhero.console.commons.feature.IFeature;
			import org.mxhero.console.commons.feature.PropertyHelper;
			import org.mxhero.console.commons.utils.RteHtmlParser;
			
			public var validationError:String;
			
			private var initialProperties:ArrayCollection;
			
			protected function creationCompleteHandler(event:FlexEvent):void
			{
				styleManager.loadStyleDeclarations("styles/mxhero.swf",true,false,ApplicationDomain.currentDomain);
				editor.textFlow  = TextConverter.importToFlow(resourceManager.getString('attachmenttrack','action.return.textarea.text'),TextConverter.PLAIN_TEXT_FORMAT);
				loadData();
			}
			
			public function isValid():Boolean{
				if(returnCb.selected){
					if(StringUtil.trim(TextConverter.export(editor.textFlow,TextConverter.PLAIN_TEXT_FORMAT,ConversionType.STRING_TYPE).toString()).length<1){
						validationError = resourceManager.getString('attachmenttrack','error.return.message');
						return false;
					}
				}
				validationError="";
				return true;
			}
			
			public function setProperties(data:ArrayCollection):void{
				this.initialProperties=data;
			}
			
			private function loadData():void{
				if(initialProperties!=null){
					var property:FeatureRuleProperty;
					property=PropertyHelper.getProperty(initialProperties,'action.selection');
					if(property!=null && property.propertyValue=='return'){
						returnCb.selected=true;
						if(PropertyHelper.getProperty(this.initialProperties,"return.message")!=null){
							editor.textFlow  = TextConverter.importToFlow(PropertyHelper.getProperty(this.initialProperties,"return.message").propertyValue,TextConverter.TEXT_FIELD_HTML_FORMAT);
						}else{
							editor.textFlow  = TextConverter.importToFlow(PropertyHelper.getProperty(this.initialProperties,"return.message.plain").propertyValue,TextConverter.PLAIN_TEXT_FORMAT);
						}
					} else {
						returnCb.selected=false;
					}
				}
			}
			
			public function getProperties():ArrayCollection{
				var properties:ArrayCollection = new ArrayCollection();
				var property:FeatureRuleProperty = new FeatureRuleProperty();
	
				if(returnCb.selected){
					property = new FeatureRuleProperty();
					property.propertyKey='action.selection';
					property.propertyValue='return';
					properties.addItem(property);
					
					property = new FeatureRuleProperty();
					property.propertyKey = "return.message";
					var t:RteHtmlParser = new RteHtmlParser();
					t.ignoreParagraphSpace=true;
					t.FormatToHTML(TextConverter.export(editor.textFlow,TextConverter.TEXT_FIELD_HTML_FORMAT,ConversionType.XML_TYPE).toString());
					var str:String = t.StringFormat;
					property.propertyValue = t.XMLFormat;
					properties.addItem(property);
					property = new FeatureRuleProperty();
					property.propertyKey = "return.message.plain";
					property.propertyValue = TextConverter.export(editor.textFlow,TextConverter.PLAIN_TEXT_FORMAT,ConversionType.STRING_TYPE).toString();
					properties.addItem(property);	
				} 
				
				property = new FeatureRuleProperty();
				property.propertyKey='locale';
				property.propertyValue=ResourceManager.getInstance().localeChain[0];
				properties.addItem(property);
				
				return properties;
			}
			
			public function getValidationError():String{
				return this.validationError;
			}
			
			public function fixedTo():Boolean{
				return true;
			}
			
			public function fixedFrom():Boolean{
				return false;
			}
			
			public function fixedToValue():String{
				return "anyone";
			}
			
			public function fixedFromValue():String{
				return "";
			}
			
			public function set applicationContext(ap:Object):void{
			}
			
			public function get applicationContext():Object{
				return null;
			}
		]]>
	</fx:Script>
	
	<fx:Metadata>
		[ResourceBundle("attachmenttrack")]
		[ResourceBundle("textLayout")]
	</fx:Metadata>
	<s:layout>
		<s:VerticalLayout  gap="20"/>
	</s:layout>
	<s:Label text="{resourceManager.getString('attachmenttrack','first.title')}" fontSize="16" fontWeight="bold"/>
	
	<s:VGroup paddingLeft="20" gap="20">
		<s:VGroup>
			<s:CheckBox id="returnCb" 
						   label="{resourceManager.getString('attachmenttrack','action.return.label')}"
						   selected="true"/>
			<s:VGroup paddingLeft="18">
				<tlf:TLFTextEditor id="editor" width="470" height="240" 
								   automaticallyExpand="false"
								   textAreaBackgroundColor="#FFFFFF"
								   color="#00000"/>			
			</s:VGroup>
		</s:VGroup>
	</s:VGroup>
</s:Module>
