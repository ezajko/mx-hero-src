<?xml version="1.0" encoding="utf-8"?>
<!--
mxHero is a platform that intends to provide a single point of development 
and single point of distribution for email solutions and enhancements. It does this
by providing an extensible framework for rapid development and deployment of
email solutions.

Copyright (C) 2012  mxHero Inc.

This program is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/>.
-->
<mx:Module xmlns:fx="http://ns.adobe.com/mxml/2009" 
		   xmlns:s="library://ns.adobe.com/flex/spark" 
		   xmlns:mx="library://ns.adobe.com/flex/mx" 		   
		   layout="vertical"
		   creationComplete="creationCompleteHandler(event)"
		   verticalGap="20"
		   paddingTop="30"
		   implements="org.mxhero.console.commons.feature.IFeature" xmlns:component="org.mxhero.console.commons.feature.component.*" xmlns:fdg="org.mxhero.console.mails.fdg.*" xmlns:components="org.flashcommander.components.*">
	
	<fx:Declarations>
		<s:RadioButtonGroup id="blockType"/>
		<mx:EmailValidator id="emailV" required="true"/>
		<mx:RegExpValidator id="domainV" required="true" expression="{Regex.domainRegex}"/>
	</fx:Declarations>
	
	<fx:Script>
		<![CDATA[
			import org.mxhero.console.commons.feature.IApplicationContext;
			import mx.utils.ObjectUtil;
			import org.mxhero.console.commons.utils.RteHtmlParser;
			import mx.collections.SortField;
			import mx.collections.Sort;
			import mx.validators.ValidationResult;
			import mx.collections.ArrayCollection;
			import mx.events.DragEvent;
			import mx.events.FlexEvent;
			import mx.managers.DragManager;
			import mx.messaging.messages.ErrorMessage;
			import mx.utils.StringUtil;
			
			import org.mxhero.console.commons.feature.FeatureRuleProperty;
			import org.mxhero.console.commons.feature.IFeature;
			import org.mxhero.console.commons.feature.PropertyHelper;
			import org.mxhero.console.commons.utils.Regex;
			
			public var validationError:String;
			
			private static const MODULE_NAME:String = "addressprotection";
			
			private var initialProperties:ArrayCollection;
			
			[Bindable]
			private var _ap:IApplicationContext;
			
			[Bindable]
			public var emails:ArrayCollection=new ArrayCollection();
			
			protected function emailAu_keyUpHandler(event:KeyboardEvent):void
			{
				_ap.refreshAccounts(emailAu.text);
			}
			
			public function accountLabelFunction(item:Object):String{
				if(item!=null){
					if(item is String){
						return item as String;
					}
					return item.account+"@"+item.domain;
				}
				return "";
			}
			
			protected function creationCompleteHandler(event:FlexEvent):void
			{
				styleManager.loadStyleDeclarations("styles/mxhero.swf",true,false,ApplicationDomain.currentDomain);
				loadData();
			}

			protected function addEmailBtt_clickHandler(event:MouseEvent):void
			{
				if(emailAu.selectedItem==null){
					emailAu.errorString=resourceManager.getString(MODULE_NAME,'email.label.expanded');
					return;
				}
				var textToValidate:String=emailAu.selectedItem.account+"@"+emailAu.selectedItem.domain;
				emailAu.errorString= "";
				
				for each(var object:Object in emails){
					if(object.toString()==textToValidate){
						emailAu.errorString=resourceManager.getString(MODULE_NAME,'duplicate.email.domain');
						return;
					}
				}
				emails.addItem(textToValidate);
				emailAu.text="";
			}
			
			public function isValid():Boolean{
				if(emails.length<1){
					validationError = resourceManager.getString(MODULE_NAME,'error.emails.empty');
					fdg.emailsDg.errorString = validationError;
					return false;
				}
				fdg.emailsDg.errorString = "";
				
				validationError="";
				return true;
			}
			
			public function setProperties(data:ArrayCollection):void{
				initialProperties=data;
			}
			
			private function loadData():void{
				if(initialProperties!=null){
					var uarray:Array = new Array();
					for each(var property:Object in PropertyHelper.getProperties(initialProperties,"email.list")){
						uarray.push(property.propertyValue);
					}
					uarray.sort();
					emails = new ArrayCollection(uarray);
					var actionSelected:String = PropertyHelper.getProperty(initialProperties,"protected.selection").propertyValue;
					if(actionSelected=='cc'){
						ccRb.selected=true;
					} else if(actionSelected=='cc.to'){
						cctoRb.selected=true;
					}
				}
			}
			
			public function getProperties():ArrayCollection{
				var properties:ArrayCollection = new ArrayCollection();
				var property:FeatureRuleProperty;
				emails.source.sort();
				for each(var object:Object in emails.source){
					property = new FeatureRuleProperty();
					property.propertyKey = "email.list";
					property.propertyValue = object.toString();
					properties.addItem(property);
				}
				
				property = new FeatureRuleProperty();
				property.propertyKey = "protected.selection";
				if(ccRb.selected){
					property.propertyValue='cc';
					properties.addItem(property);
				}else if(cctoRb.selected){
					property.propertyValue='ccto';
					properties.addItem(property);					
				}
				
				return properties;
			}
			
			public function getValidationError():String{
				return this.validationError;
			}
			
			public function fixedTo():Boolean{
				return true;
			}
			
			public function fixedFrom():Boolean{
				return false;
			}
			
			public function fixedToValue():String{
				return "anyoneelse";
			}
			
			public function fixedFromValue():String{
				return "";
			}
			
			public function set applicationContext(ap:Object):void{
				this._ap=ap as IApplicationContext;
			}
			
			public function get applicationContext():Object{
				return _ap;
			}
		]]>
	</fx:Script>
	
	<fx:Metadata>
		[ResourceBundle("addressprotection")]
	</fx:Metadata>

	<s:Label text="{resourceManager.getString(MODULE_NAME,'first.title')}" fontSize="16" fontWeight="bold"/>
	
	<s:VGroup height="100%" width="400" verticalAlign="bottom" paddingLeft="20">
		
		<mx:Form defaultButton="{addEmailBtt}" 
				 paddingLeft="0" paddingRight="0" 
				 paddingTop="0" paddingBottom="0"
				 width="100%">
			<component:LabelExpanded
				labelText="{resourceManager.getString(MODULE_NAME,'email.label.text')}"
				expandedText="{resourceManager.getString(MODULE_NAME,'email.label.expanded')}"
				explainText="{resourceManager.getString(MODULE_NAME,'label.explain')}"
				hideText="{resourceManager.getString(MODULE_NAME,'label.hide')}"
				width="100%"/>
			<s:HGroup width="100%">
				<components:AutoComplete
					id="emailAu"
					width="100%"
					keyUp="emailAu_keyUpHandler(event)"
					requireSelection="false"
					prefixOnly="true"
					dataProvider="{_ap.accounts}"
					labelFunction="{accountLabelFunction}"
					forceOpen="true"
					/>
				<s:Button id="addEmailBtt" 
						  label="{resourceManager.getString(MODULE_NAME,'email.add')}"
						  buttonMode="true"
						  click="addEmailBtt_clickHandler(event)"/>							
			</s:HGroup>
		</mx:Form>
		
		<fdg:FilterDataGrid id="fdg" dataProvider="{emails}" width="100%"/>	

		<mx:Spacer height="10"/>
		
	</s:VGroup>
	
	<s:Label text="{resourceManager.getString(MODULE_NAME,'second.title')}" fontSize="16" fontWeight="bold"/>
	
	<s:VGroup paddingLeft="20" gap="10">
		<s:RadioButton id="ccRb" 
						   groupName="blockType"
						   label="{resourceManager.getString(MODULE_NAME,'action.cc.label')}"
						   selected="true"/>
		<s:RadioButton id="cctoRb" 
					   groupName="blockType"
					   label="{resourceManager.getString(MODULE_NAME,'action.cc.to.label')}"/>
	</s:VGroup>
</mx:Module>
