<?xml version="1.0" encoding="utf-8"?>
<mx:ModuleLoader xmlns:fx="http://ns.adobe.com/mxml/2009" 
				 xmlns:s="library://ns.adobe.com/flex/spark" 
				 xmlns:mx="library://ns.adobe.com/flex/mx"
				 error="errorHandler(event)"
				 progress="progressHandler(event)"
				 loading="loadingHandler(event)"
				 ready="readyHandler(event)" 
				 unload="moduleloader1_unloadHandler(event)"
				 xmlns:component="org.mxhero.console.commons.component.*"
				 creationComplete="creationCompleteHandler(event)" 
				 xmlns:component1="org.mxhero.console.commons.feature.component.*" 
				 xmlns:parsley="http://www.spicefactory.org/parsley"
				 applicationDomain="{ApplicationDomain.currentDomain}">
	<fx:Script>
		<![CDATA[
			import mx.controls.Alert;
			import mx.events.FlexEvent;
			import mx.events.ModuleEvent;
			
			import org.mxhero.console.commons.feature.IFeature;
			import org.mxhero.console.features.application.resources.RuleViewProperties;
			import org.mxhero.console.features.presentation.rule.RuleViewPM;

			[Bindable]
			public var featureModule:IFeature;
			
			[Inject]
			[Bindable]
			public var model:RuleViewPM;
			
			[Bindable]
			public var loaded:Boolean=false;
			
			protected function errorHandler(event:ModuleEvent):void
			{
				progress.label=resourceManager.getString(RuleViewProperties.NAME,RuleViewProperties.FEATURE_MODULE_ERROR);
				loaded=false;
			}

			protected function progressHandler(event:ModuleEvent):void
			{
				if(progress!=null){
					progress.label=resourceManager.getString(RuleViewProperties.NAME,RuleViewProperties.FEATURE_MODULE_LOADING)
						+" "+Math.round((event.bytesLoaded/event.bytesTotal) * 100) +"%";
				}
			}

			protected function loadingHandler(event:Event):void
			{
				loaded=false;
				this.applicationDomain=ApplicationDomain.currentDomain;
				currentState="loading";
				this.featureModule=null;
			}

			protected function readyHandler(event:ModuleEvent):void
			{
				this.featureModule= this.child as IFeature;
				currentState="loaded";
				if(featureModule!=null){
					this.featureModule.setProperties(model.rule.properties);
				}
				loaded=true;
				model.refresh(false);
			}

			protected function creationCompleteHandler(event:FlexEvent):void
			{
				RuleViewPM.realoadExternal=this.reload;
			}

			public function reload():void{
				this.unloadModule();
				this.loadModule();
			}
			

			protected function moduleloader1_unloadHandler(event:ModuleEvent):void
			{
				loaded=false;
			}

		]]>
	</fx:Script>
	
	<fx:Declarations>
		<parsley:Configure/>
	</fx:Declarations>
	
	<mx:states>
		<s:State name="loading"/>
		<s:State name="loaded"/>
	</mx:states>
	
	<mx:ProgressBar id="progress" 
					source="{this}" 
					excludeFrom="loaded"
					color="#FFFFFF"
					labelPlacement="center"/>
</mx:ModuleLoader>
